
#Область ОбработчикиСобытийФормы

 &НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОтобразитьЗаявкиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_ВКМ_ОбслуживаниеКлиента" Тогда
		ОтобразитьЗаявкиНаСервере();
	КонецЕсли;
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ДатаПроведенияРабот", Начало);
	ЗначенияЗаполнения.Вставить("ВремяНачалаРабот", Начало);
	ЗначенияЗаполнения.Вставить("ВремяОкончанияРабот", Конец);
	ЗначенияЗаполнения.Вставить("Специалист", ЗначенияИзмерений["Специалист"]);
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиента.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтобразитьЗаявки(Команда)
	ОтобразитьЗаявкиНаСервере();
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

   &НаСервере
Процедура ОтобразитьЗаявкиНаСервере()
	
	НачалоПериода = НачалоМесяца(ТекущаяДатаСеанса());
	КонецПериода = КонецМесяца(ТекущаяДатаСеанса());
	
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоПериода, КонецПериода); 
	ИзмеренияПланировщика = Планировщик.Измерения;
	ИзмеренияПланировщика.Очистить();
	ИзмерениеСпециалист = ИзмеренияПланировщика.Добавить("Специалист");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник КАК Сотрудник,
	               |	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник.Наименование КАК СотрудникНаименование
	               |ИЗ
	               |	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата, Категория = &Категория) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних";
	
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Категория", Перечисления.ВКМ_КатегорииСотрудников.Специалист);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ИзмерениеСпециалист.Элементы.Добавить(Выборка.Сотрудник, Выборка.СотрудникНаименование);	
	КонецЦикла;
	
	Планировщик.Элементы.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ОбслуживаниеКлиента.Специалист КАК Специалист,
	|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот КАК ДатаПроведенияРабот,
	|	ВКМ_ОбслуживаниеКлиента.ВремяНачалаРабот КАК ВремяНачалаРабот,
	|	ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРабот КАК ВремяОкончанияРабот,
	|	ВКМ_ОбслуживаниеКлиента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
	|ГДЕ
	|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот МЕЖДУ &ДатаНачала И &ДатаОкончания"; 
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоПериода);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецПериода);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачалоРабот = Дата(Год(Выборка.ДатаПроведенияРабот),Месяц(Выборка.ДатаПроведенияРабот),День(Выборка.ДатаПроведенияРабот),
		Час(Выборка.ВремяНачалаРабот), Минута(Выборка.ВремяНачалаРабот), Секунда(Выборка.ВремяНачалаРабот));
		КонецРабот = Дата(Год(Выборка.ДатаПроведенияРабот),Месяц(Выборка.ДатаПроведенияРабот),День(Выборка.ДатаПроведенияРабот),
		Час(Выборка.ВремяОкончанияРабот), Минута(Выборка.ВремяОкончанияРабот), Секунда(Выборка.ВремяОкончанияРабот));
		
		СоответствиеЗначений = Новый Соответствие;
		СоответствиеЗначений.Вставить("Специалист", Выборка.Специалист);
		
		ЭлементПланировщика = Планировщик.Элементы.Добавить(НачалоРабот,КонецРабот);
		ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(СоответствиеЗначений);
		ЭлементПланировщика.Значение = Выборка.ССылка;
		ЭлементПланировщика.Текст = Выборка.Специалист;
			
	КонецЦикла; 
	
КонецПроцедуры 

#КонецОбласти
