 
 #Область ОбработчикиКомандФормы 
 
  &НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРеализации(Команда)
	
	Если Объект.РеализацииПоДоговорам.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Для проведения операции необходимо заполнить Табличную часть.");
		Возврат;
	КонецЕсли; 
	
	Договоры = Новый Массив;
	
	Для Каждого Строка ИЗ Объект.РеализацииПоДоговорам Цикл
		Если НЕ ЗначениеЗаполнено(Строка.Реализация) Тогда
			Договоры.Добавить(Строка.Договор);
		 КонецЕсли;
	 КонецЦикла;

	ДлительнаяОперация = СоздатьРеализацииНаСервере(Договоры);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Интервал = 1;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОповещениеОЗавершении", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания); 
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Объект.РеализацииПоДоговорам.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	               |ПОМЕСТИТЬ ВТ_ДействующиеДоговоры
	               |ИЗ
	               |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	               |ГДЕ
	               |	ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействия < &ДатаОкончанияПериода
	               |	И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействия > &ДатаНачалаПериода
	               |	И ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	РеализацияТоваровУслуг.Ссылка КАК Реализация,
	               |	РеализацияТоваровУслуг.Договор КАК Договор
	               |ПОМЕСТИТЬ ВТ_РеализацииЗаПериод
	               |ИЗ
	               |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	               |ГДЕ
	               |	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачалаПериода И &ДатаОкончанияПериода
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ДействующиеДоговоры.Ссылка КАК Договор,
	               |	ВТ_РеализацииЗаПериод.Реализация КАК Реализация
	               |ИЗ
	               |	ВТ_ДействующиеДоговоры КАК ВТ_ДействующиеДоговоры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РеализацииЗаПериод КАК ВТ_РеализацииЗаПериод
	               |		ПО ВТ_ДействующиеДоговоры.Ссылка = ВТ_РеализацииЗаПериод.Договор";
	
	 Запрос.УстановитьПараметр("ДатаНачалаПериода", НачалоМесяца(Объект.Период));
	 Запрос.УстановитьПараметр("ДатаОкончанияПериода", КонецМесяца(Объект.Период));
	 Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание);
	 
	 Выборка = Запрос.Выполнить().Выбрать();
	 
	 Пока Выборка.Следующий() Цикл
		 НоваяСтрока = Объект.РеализацииПоДоговорам.Добавить();
		 НоваяСтрока.Договор = Выборка.Договор;
		 НоваяСтрока.Реализация = Выборка.Реализация;		 
	 КонецЦикла;
	 
КонецПроцедуры

&НаСервере
Функция  СоздатьРеализацииНаСервере(Договоры)
  ДлительнаяОперация = ДлительныеОперации.ВыполнитьПроцедуру(, "Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьРеализацииПоДоговорам",Объект.Период, Договоры);
  Возврат ДлительнаяОперация;
КонецФункции

&НаКлиенте
Процедура ОповещениеОЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Выполнено" Тогда
		ЗаполнитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти
